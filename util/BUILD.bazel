load("@bazel_gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@buildifier_prebuilt//:rules.bzl", "buildifier", "buildifier_test")
load("@rules_multirun//:defs.bzl", "command", "multirun")

buildifier(
    name = "buildifier.fix",
    diff_command = "diff",
    exclude_patterns = ["./.git/*"],
    lint_mode = "fix",
    mode = "fix",
)

buildifier_test(
    name = "buildifier.check",
    size = "small",
    diff_command = "diff",
    exclude_patterns = ["./.git/*"],
    lint_mode = "warn",
    mode = "diff",
    no_sandbox = True,
    tags = ["no-remote-exec"],
    workspace = "//:WORKSPACE",
)

multirun(
    name = "update",
    commands = [
        ":gazelle",
        ":update_filegroups",
        ":update_deleted_packages",
        ":buildifier.fix",
    ] + select({
        # Keep in sync with //docs:BUILD.bazel.
        "@platforms//os:windows": [],
        "//docs:do_not_build_docs": [],
        "//conditions:default": ["//docs:update-alias"],
    }),
)

gazelle_binary(
    name = "gazelle_bin",
    languages = ["@bazel_skylib_gazelle_plugin//bzl"],
)

gazelle(
    name = "gazelle",
    gazelle = "gazelle_bin",
)

py_binary(
    name = "update_filegroups",
    srcs = ["update_filegroups.py"],
    args = [
        "--buildozer",
        "$(rlocationpath @buildifier_prebuilt//:buildozer)",
    ] + select({
        "@aspect_bazel_lib//lib:bzlmod": ["--enable_bzlmod"],
        "//conditions:default": [],
    }),
    data = ["@buildifier_prebuilt//:buildozer"],
    deps = ["@rules_python//python/runfiles"],
)

command(
    name = "update_deleted_packages",
    command = "@rules_bazel_integration_test//tools:update_deleted_packages",
    environment = {"LC_COLLATE": "C.UTF-8"},
)

py_binary(
    name = "update_zig_versions",
    srcs = ["update_zig_versions.py"],
    args = [
        "--output",
        "$(rootpath //zig/private:versions.json)",
        "--template-bzl",
        "$(rootpath //zig/private:versions.bzl.tpl)",
        "--output-bzl",
        "$(rootpath //zig/private:versions.bzl)",
    ],
    data = [
        "//zig/private:versions.bzl",
        "//zig/private:versions.bzl.tpl",
        "//zig/private:versions.json",
    ],
)
