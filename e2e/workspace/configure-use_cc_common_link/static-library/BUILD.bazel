load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@rules_cc//cc:defs.bzl", "cc_library")
load("@rules_zig//zig:defs.bzl", "zig_binary", "zig_configure", "zig_configure_binary", "zig_configure_test", "zig_static_library", "zig_test")

cc_library(
    name = "add",
    srcs = ["add.c"],
    linkstatic = True,
)

zig_binary(
    name = "binary",
    main = "main.zig",
    tags = ["manual"],
    deps = [":add"],
)

zig_configure_binary(
    name = "binary_linked_with_cc_common_link",
    actual = ":binary",
    use_cc_common_link = True,
)

zig_static_library(
    name = "library",
    main = "main.zig",
    tags = ["manual"],
    deps = [":add"],
)

zig_configure(
    name = "library_linked_with_cc_common_link",
    actual = ":library",
    use_cc_common_link = True,
)

zig_test(
    name = "test",
    size = "small",
    main = "main.zig",
    tags = ["manual"],
    deps = [":add"],
)

zig_configure_test(
    name = "test_linked_with_cc_common_link",
    size = "small",
    actual = ":test",
    use_cc_common_link = True,
)

genrule(
    name = "output",
    outs = ["output.actual"],
    cmd = "$(execpath :binary_linked_with_cc_common_link) > $(OUTS)",
    tools = [":binary_linked_with_cc_common_link"],
)

diff_test(
    name = "output_test",
    size = "small",
    file1 = ":output.expected",
    file2 = ":output.actual",
)

genrule(
    name = "library-symbol",
    srcs = [":library_linked_with_cc_common_link"],
    outs = ["library-symbol.txt"],
    # Test that `add` is not inlined into the Zig library, but remains an
    # undefined symbol to be resolved later.
    cmd = "$(NM) --undefined-only $(SRCS) | grep add > $(OUTS)",
    toolchains = ["@bazel_tools//tools/cpp:current_cc_toolchain"],
)

build_test(
    name = "build",
    size = "small",
    targets = [
        ":binary_linked_with_cc_common_link",
        ":library_linked_with_cc_common_link",
        ":library-symbol",
        ":test_linked_with_cc_common_link",
    ],
)
