load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@rules_cc//cc:defs.bzl", "cc_library")
load("@rules_zig//zig:defs.bzl", "zig_binary", "zig_configure", "zig_configure_binary", "zig_configure_test", "zig_shared_library", "zig_static_library", "zig_test")

cc_library(
    name = "add",
    srcs = ["add.c"],
    linkstatic = True,
)

zig_binary(
    name = "binary",
    main = "main.zig",
    tags = ["manual"],
    deps = [":add"],
)

zig_configure_binary(
    name = "binary_linked_with_cc_common_link",
    actual = ":binary",
    target_compatible_with = select({
        # Zig 0.15.1 sometimes creates malformed archives
        # https://github.com/ziglang/zig/issues/25069
        "@zig_toolchains//:0.15.2": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    use_cc_common_link = 1,
)

zig_static_library(
    name = "static-library",
    main = "main.zig",
    tags = ["manual"],
    deps = [":add"],
)

zig_configure(
    name = "static_library_linked_with_cc_common_link",
    actual = ":static-library",
    target_compatible_with = select({
        # Zig 0.15.1 sometimes creates malformed archives
        # https://github.com/ziglang/zig/issues/25069
        "@zig_toolchains//:0.15.2": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    use_cc_common_link = 1,
)

zig_shared_library(
    name = "shared-library",
    main = "main.zig",
    tags = ["manual"],
    deps = [":add"],
)

zig_configure(
    name = "shared_library_linked_with_cc_common_link",
    actual = ":shared-library",
    target_compatible_with = select({
        # Zig 0.15.1 sometimes creates malformed archives
        # https://github.com/ziglang/zig/issues/25069
        "@zig_toolchains//:0.15.2": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    use_cc_common_link = 1,
)

zig_test(
    name = "test",
    size = "small",
    main = "main.zig",
    tags = ["manual"],
    deps = [":add"],
)

zig_configure_test(
    name = "test_linked_with_cc_common_link",
    size = "small",
    actual = ":test",
    target_compatible_with = select({
        # Zig 0.15.1 sometimes creates malformed archives
        # https://github.com/ziglang/zig/issues/25069
        "@zig_toolchains//:0.15.2": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    use_cc_common_link = 1,
)

genrule(
    name = "output",
    outs = ["output.actual"],
    cmd = "$(execpath :binary_linked_with_cc_common_link) > $(OUTS)",
    tools = [":binary_linked_with_cc_common_link"],
)

diff_test(
    name = "output_test",
    size = "small",
    file1 = ":output.expected",
    file2 = ":output.actual",
)

genrule(
    name = "static-library-symbol",
    srcs = [":static_library_linked_with_cc_common_link"],
    outs = ["library-symbol.txt"],
    # Test that `add` is not inlined into the Zig library, but remains an
    # undefined symbol to be resolved later.
    cmd = "$(NM) --undefined-only $(SRCS) | grep add > $(OUTS)",
    toolchains = ["@bazel_tools//tools/cpp:current_cc_toolchain"],
)

build_test(
    name = "build",
    size = "small",
    targets = [
        ":binary_linked_with_cc_common_link",
        ":shared_library_linked_with_cc_common_link",
        ":static_library_linked_with_cc_common_link",
        ":static-library-symbol",
        ":test_linked_with_cc_common_link",
    ],
)
