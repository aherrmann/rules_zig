load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@rules_zig//zig:defs.bzl", "zig_binary", "zig_test")

zig_binary(
    name = "binary",
    data = ["data.txt"],
    env = {"DATA": "$(rlocationpath data.txt)"},
    main = "main.zig",
    deps = ["@rules_zig//zig/runfiles"],
)

genrule(
    name = "output",
    srcs = [":data.txt"],
    outs = ["output.actual"],
    cmd = "DATA=$(rlocationpath data.txt) $(execpath :binary) > $(OUTS)",
    tools = [":binary"],
)

diff_test(
    name = "output_test",
    size = "small",
    file1 = ":output.expected",
    file2 = ":output.actual",
)

zig_test(
    name = "test",
    size = "small",
    data = [
        "data.txt",
        ":binary",
        "@runfiles_library_dependency//:data.txt",
    ],
    env = {
        "BINARY": "$(rlocationpath :binary)",
        "DATA": "$(rlocationpath data.txt)",
        # Intentionally not using $(rlocationpath ...) to test repo-mapping.
        "DEPENDENCY_DATA": "runfiles_library_dependency/data.txt",
    },
    main = "main.zig",
    deps = [
        "@rules_zig//zig/runfiles",
        "@runfiles_library_dependency//:module_with_data",
    ],
)

build_test(
    name = "runfiles-docs-build-test",
    tags = ["zig-docs"],
    target_compatible_with = select({
        # Flaky with older versions and on MacOS, see
        # https://github.com/aherrmann/rules_zig/issues/273
        "@platforms//os:macos": ["@platforms//:incompatible"],
        "@zig_toolchains//:0.11.0": ["@platforms//:incompatible"],
        "@zig_toolchains//:0.12.0": ["@platforms//:incompatible"],
        "@zig_toolchains//:0.12.1": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    targets = ["@rules_zig//zig/runfiles:docs"],
)

test_suite(
    name = "runfiles-library",
    tests = [
        ":runfiles-docs-build-test",
        ":test",
        "@runfiles_library_dependency//:test",
    ],
)
