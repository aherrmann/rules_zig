load("@bazel_skylib//lib:selects.bzl", "selects")
load(
    "@rules_zig//zig:defs.bzl",
    "zig_library",
    "zig_module",
    "zig_test",
)

selects.config_setting_group(
    name = "macos-zig-0.12.0",
    match_all = [
        "@platforms//os:macos",
        "@zig_toolchains//:0.12.0",
    ],
)

_SRCS = [
    "src/Directory.zig",
    "src/discovery.zig",
    "src/Manifest.zig",
    "src/RepoMapping.zig",
    "src/RPath.zig",
    "src/Runfiles.zig",
]

zig_module(
    name = "runfiles",
    srcs = _SRCS,
    main = "runfiles.zig",
    visibility = ["//visibility:public"],
)

zig_test(
    name = "test",
    size = "small",
    srcs = _SRCS,
    cdeps = ["//zig/lib:libc"],
    data = ["test-data.txt"],
    main = "runfiles.zig",
)

# Only used for documentation generation.
zig_library(
    name = "lib",
    srcs = _SRCS,
    extra_docs = ["guide.md"],
    main = "runfiles.zig",
    tags = ["manual"],
)

filegroup(
    name = "docs",
    srcs = [":lib"],
    output_group = "zig_docs",
    target_compatible_with = select({
        ":macos-zig-0.12.0": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

# Execute `bazel run //util:update_filegroups` to update this target.
filegroup(
    name = "all_files",
    srcs = [
        ":BUILD.bazel",
        ":guide.md",
        ":runfiles.zig",
        ":src/Directory.zig",
        ":src/Manifest.zig",
        ":src/RPath.zig",
        ":src/RepoMapping.zig",
        ":src/Runfiles.zig",
        ":src/discovery.zig",
        ":test-data.txt",
    ],
    visibility = ["//zig:__pkg__"],
)
