"""Implementation of the zig_test rule."""

load(
    "//zig/private/common:zig_build.bzl",
    "TEST_ATTRS",
    "zig_build_impl",
    COMMON_ATTRS = "ATTRS",
    COMMON_TOOLCHAINS = "TOOLCHAINS",
)
load(
    "//zig/private/common:zig_docs.bzl",
    "zig_docs_impl",
    DOCS_ATTRS = "ATTRS",
)

DOC = """\
Builds a Zig test.

The target can be executed using `bazel test`, corresponding to `zig test`.

Documentation can be generated by requesting the `zig_docs` output group, e.g.
using the command `bazel build //my:target --output_groups=zig_docs`.

**EXAMPLE**

```bzl
load("@rules_zig//zig:defs.bzl", "zig_test")

zig_test(
    name = "my-test",
    main = "test.zig",
    srcs = [
        "utils.zig",  # to support `@import("utils.zig")`.
    ],
    deps = [
        ":my-module",  # to support `@import("my-module")`.
    ],
)
```
"""

ATTRS = COMMON_ATTRS | TEST_ATTRS | DOCS_ATTRS

TOOLCHAINS = COMMON_TOOLCHAINS

def _zig_test_impl(ctx):
    build, build_groups = zig_build_impl(ctx, kind = "zig_test")
    docs, docs_groups = zig_docs_impl(ctx, kind = "zig_test")
    return build + docs + [OutputGroupInfo(**(build_groups | docs_groups))]

zig_test = rule(
    _zig_test_impl,
    attrs = ATTRS,
    doc = DOC,
    test = True,
    toolchains = TOOLCHAINS,
)
