"""Implementation of the zig_shared_library rule."""

load(
    "//zig/private/common:zig_build.bzl",
    "COMMON_LIBRARY_ATTRS",
    "SHARED_LIBRARY_ATTRS",
    "zig_build_impl",
    COMMON_ATTRS = "ATTRS",
    COMMON_FRAGMENTS = "FRAGMENTS",
    COMMON_TOOLCHAINS = "TOOLCHAINS",
)
load(
    "//zig/private/common:zig_docs.bzl",
    "zig_docs_impl",
    DOCS_ATTRS = "ATTRS",
)

DOC = """\
Builds a Zig shared library, produces a shared or dynamic library.

The target can be built using `bazel build`, corresponding to `zig build-lib`.

Suitable as a dependency to C/C++ targets.

Documentation can be generated by requesting the `zig_docs` output group, e.g.
using the command `bazel build //my:target --output_groups=zig_docs`.

**EXAMPLE**

```bzl
load("@rules_zig//zig:defs.bzl", "zig_shared_library")

zig_shared_library(
    name = "my-library",
    main = "main.zig",
    srcs = [
        "utils.zig",  # to support `@import("utils.zig")`.
    ],
    deps = [
        ":my-module",  # to support `@import("my-module")`.
    ],
)
```
"""

ATTRS = COMMON_ATTRS | COMMON_LIBRARY_ATTRS | SHARED_LIBRARY_ATTRS | DOCS_ATTRS

TOOLCHAINS = COMMON_TOOLCHAINS

FRAGMENTS = COMMON_FRAGMENTS

def _zig_shared_library_impl(ctx):
    build, build_groups = zig_build_impl(ctx, kind = "zig_shared_library")
    docs, docs_groups = zig_docs_impl(ctx, kind = "zig_shared_library")
    return build + docs + [OutputGroupInfo(**(build_groups | docs_groups))]

zig_shared_library = rule(
    _zig_shared_library_impl,
    attrs = ATTRS,
    doc = DOC,
    toolchains = TOOLCHAINS,
    fragments = FRAGMENTS,
)
